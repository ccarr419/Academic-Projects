# programqsort1p.txt

int [] qsort = function(int [] iarray) {
    int parallelThreshold = 3750 ;
    # Any array whose lentgh is <= parallelThreshold is sorted serially.
    int getMedian = function(int [] subarray, int moe, int larry, int cheese) {
        return ((subarray[moe] >= subarray[larry]
                    && subarray[larry] >= subarray[cheese]
                || subarray[cheese] >= subarray[larry]
                    && subarray[larry] >= subarray[moe]) ? larry
            : ((subarray[larry] >= subarray[moe]
                    && subarray[moe] >= subarray[cheese]
                || subarray[cheese] >= subarray[moe]
                    && subarray[moe] >= subarray[larry]) ? moe : cheese));
    }
    int [] makelist = function(int v) {
        # We need a function to inject the median, at least until reduce
        # permits variables.
        return [v];
    }
    int [] partition = function(int [] inarray, int pivotix,
            int position, int [] leftarray, int [] rightarray) {
        return(position >= length(inarray)
            ? ((length(leftarray)+length(rightarray) <= parallelThreshold)
                ? (qsort(leftarray) + [inarray[pivotix]] + qsort(rightarray))
                : {{ reduce +
                    qsort(leftarray); makelist(inarray[pivotix]);
                    qsort(rightarray);}})
            : (position == pivotix)
                ? partition(inarray, pivotix, position+1, leftarray, rightarray)
                : ((inarray[position] <= inarray[pivotix])
                    ? partition(inarray, pivotix, position+1,
                        leftarray + [inarray[position]], rightarray)
                    : partition(inarray, pivotix, position+1,
                        leftarray, rightarray + [inarray[position]])));
    }
    return ((length(iarray) < 2) ? iarray
        : partition(iarray, getMedian(iarray, 0, length(iarray)/2,
                                        length(iarray)-1), 0, [], []));
}

string printiarray = function(int [] iarray, int i) {
    return (i > -1 && i < length(iarray))
        ? (iarray[i] ~ printiarray(iarray, i+1))
        : ' ';
}

int [] makeBigArray = function(int bound) {
    int [] helpmakeArray = function(int thcount, int frontier, int bound, int [] sofar) {
        int usethreads = 16 ;
        return (frontier >= bound) ? sofar
            : (thcount >= usethreads)
                ? helpmakeArray(thcount, frontier + 1, bound, sofar + [frontier, -frontier])
                : sofar + {{ reduce + helpmakeArray(thcount*2, frontier, bound/2, []);
                    helpmakeArray(thcount*2, bound/2, bound, []); }} ; }
    return helpmakeArray(1, 0, bound, []);
}

int [] iarr = makeBigArray(60000);
'pre-qs';
printiarray(iarr, 0);
'qs';
printiarray(qsort(iarr), 0);
